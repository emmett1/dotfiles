pkgname=dwm
pkgver=6.0
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'rxvt-unicode')
install=dwm.install
source=(http://dl.suckless.org/dwm/dwm-$pkgver.tar.gz
	config.h
	dwm.desktop
	01-tilegap.diff
        02-centredfloating.diff
        03-systray.diff
        04-pertag.diff
        05-attachabove.diff
        06-focusadjacenttag3.diff
        07-pushnomaster.diff
        08-save_floats.diff
	terminus2.bdf)
md5sums=('8bb00d4142259beb11e13473b81c0857'
         '55fa8b1fbed024335a6d6da894060079'
         '939f403a71b6e85261d09fc3412269ee'
         '3ff127caca94595198c65c8630e2e074'
         'ed11483bfccbf65ff9714c0ca4e7bb23'
         '93e63585312f1ac5bcd32b347c6beda8'
         'be94530c8592342bd99c7b5eeafdd176'
         'ec8cb48d437ff14589d5a6d4b8d4163b'
         '8114ef7b7cdd66e4c1b0d37eb23888fd'
         '6805feec564612ed81a244c3e9c52e6e'
         '44d0c475358e5653c302c0f5b2d12f28'
         'f2324c7c9ed1102dad374b067c20d95a')

prepare() {
	cd $pkgname-$pkgver

	cp ../config.h config.h

	patch -Np1 -i ../01-tilegap.diff
	patch -Np1 -i ../02-centredfloating.diff
	patch -Np1 -i ../03-systray.diff
	patch -Np1 -i ../04-pertag.diff
	patch -Np1 -i ../05-attachabove.diff
	patch -Np1 -i ../06-focusadjacenttag3.diff
	patch -Np1 -i ../07-pushnomaster.diff
	patch -Np1 -i ../08-save_floats.diff
}

build() {
	cd $pkgname-$pkgver
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make PREFIX=/usr DESTDIR=$pkgdir install
	install -m644 -D LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
	install -m644 -D README $pkgdir/usr/share/doc/$pkgname/README
	install -m644 -D $srcdir/dwm.desktop $pkgdir/usr/share/xsessions/dwm.desktop
	install -m644 -D $srcdir/terminus2.bdf $pkgdir/usr/share/fonts/X11/misc/terminus2.bdf
}
